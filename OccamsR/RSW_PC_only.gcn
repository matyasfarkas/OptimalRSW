## ############################################################################
# (c) 3Eq NK model a la Rotember Woodford model with MS Regimes              #
#                                                                            #
# Authors:  Matyas Farkas, ECB and Kai Christoffel, ECB                      #
# ############################################################################
# Constant probability case
# ############################################################################

options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
    output LaTeX landscape = TRUE;
};

# tryreduce
#  {
#    piH[], piL[], yH[], yL[];
#  };

block OPTIMALMP
{
    definitions
    {
        uH[] = -1/2* ((piH[]+pitH-pitCB)^2 + kappa/theta * yH[]*yH[]);
        uL[] = -1/2* ((piL[]+pitL-pitCB)^2 + kappa/theta * yL[]*yL[]);
    };
    controls
    {
         piH[],piL[], yH[],   yL[], i[]; #,   yH[],   yL[]
    };
    objective
    {
                U[] = 0.5*( uL[]+uH[]) + beta * E[][U[1]] ;
    };
    constraints
    {
        piH[-1]= kappa* yH[-1] + beta* (pH*piH[]+ (1-pH)*piL[])  + log(etapi[-1])  ;
        piL[-1]= kappa* yL[-1] + beta* (pL*piL[]+ (1-pL)*piH[]) + log(etapi[-1]) ;
        yH[-1] = (pH*yH[]+ (1-pH)*yL[]) - sigma* ((i[-1] - (pH*piH[] + (1-pH)*piL[])));  
        yL[-1] = (pL*yL[]+ (1-pL)*yH[]) - sigma* ((i[-1] - (pL*piL[] + (1-pL)*piH[])));
    };
    calibration
    {
        kappa =  0.2465; # (1-alpha)(1-alpha*beta)/(alpha*(1+eta*theta)) * ((sigma^-1) + eta), where alpha =0.33, beta =0.99, sigma =2, theta=6
        beta = 0.99;
        theta = 6;
        sigma = 1;
        pH = 0.99;
        pL = 0.99;
        pitCB = 2;
        pitH  = 2;
        pitL = 4;
    };
};

block EXOG 
{
    identities
    {
        etapi[] = exp(phi * log(etapi[-1]) + epsilon_pi[]);
    };
    shocks
    {
        epsilon_pi[];
    };
    calibration
    {
        phi = 0.95;
    };
};
